import os

vars = Variables("custom.py")
vars.Add("VC_INCLUDE_PATH", "include files of the Vc vectorization library", '')
vars.Add("VC_LIBRARY_PATH", "path to shared libararies of the Vc vectorization library", '')
vars.Add("BOOST_INCLUDE_PATH_RELEASE", "", '')
vars.Add("BOOST_LIBRARY_PATH_RELEASE", "", '')
vars.Add("BOOST_INCLUDE_PATH_DEBUG", "", '')
vars.Add("BOOST_LIBRARY_PATH_DEBUG", "", '')
vars.Add("CC", "C compiler", 'gcc')
vars.Add("CXX", "C++ compiler", 'g++')

env_release = Environment(variables=vars, ENV=os.environ)
env_release.AppendUnique(CPPFLAGS=['-fopenmp', '-Wall', '-Wno-ignored-attributes', '-Wno-unused-local-typedefs'])
env_release.AppendUnique(LINKFLAGS=['-fopenmp'])
env_release.AppendUnique(CPPPATH=[env_release["VC_INCLUDE_PATH"]])
env_release.AppendUnique(LIBPATH=[env_release["VC_LIBRARY_PATH"]])
env_release.AppendUnique(LIBS=['Vc', 'boost_program_options'])


env_debug = env_release.Clone()

env_release.AppendUnique(CPPFLAGS=['-O3', '-ffast-math', '-mavx2', '-mfma', '-g'])
env_release.AppendUnique(CPPPATH=[env_release["BOOST_INCLUDE_PATH_RELEASE"]])
env_release.AppendUnique(LIBPATH=[env_release["BOOST_LIBRARY_PATH_RELEASE"]])

env_debug.AppendUnique(CPPFLAGS=['-O0', '-g', '-fno-omit-frame-pointer'])
env_debug.AppendUnique(CPPPATH=[env_debug["BOOST_INCLUDE_PATH_DEBUG"]])
env_debug.AppendUnique(LIBPATH=[env_debug["BOOST_LIBRARY_PATH_DEBUG"]])

SConscript('SConscript', variant_dir='release', exports={'env':env_release}, duplicate=0)
SConscript('SConscript', variant_dir='debug', exports={'env':env_debug}, duplicate=0)
